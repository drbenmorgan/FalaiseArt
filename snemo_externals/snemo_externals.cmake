# Have to vendor Bayeux+CAMP dep temporarily...
include(ExternalProject)
include(ProcessorCount)

ExternalProject_Add(CAMP_EP
  GIT_REPOSITORY https://github.com/drbenmorgan/camp.git
  GIT_TAG 7564e57f7b406d1021290cf2260334d57d8df255
  UPDATE_COMMAND ""
  INSTALL_DIR ${PROJECT_BINARY_DIR}/BuildProducts
  CMAKE_CACHE_ARGS
    -DCMAKE_INSTALL_PREFIX:PATH=<INSTALL_DIR>
    -DCMAKE_BUILD_TYPE:STRING=${CMAKE_BUILD_TYPE}
    -DCMAKE_CXX_FLAGS:STRING=${CMAKE_CXX_FLAGS}
    -DCMAKE_CXX_FLAGS_RELEASE:STRING=${CMAKE_CXX_FLAGS_RELEASE}
    -DCMAKE_CXX_FLAGS_DEBUG:STRING=${CMAKE_CXX_FLAGS_DEBUG}
    -DCMAKE_CXX_FLAGS_OPT:STRING=${CMAKE_CXX_FLAGS_OPT}
    -DCMAKE_CXX_FLAGS_PROF:STRING=${CMAKE_CXX_FLAGS_PROF}
  )

ExternalProject_Add(BAYEUX_EP
  URL https://github.com/SuperNEMO-DBD/Bayeux/archive/3.3.0.tar.gz
  URL_HASH SHA256=6468251da50214e744651260770bf252f677a8f9b9f822085c38dc69d71b52a9
  UPDATE_COMMAND ""
  PATCH_COMMAND patch -p1 < ${CMAKE_CURRENT_LIST_DIR}/bayeux-3.3.0.patch
  INSTALL_DIR ${PROJECT_BINARY_DIR}/BuildProducts
  CMAKE_CACHE_ARGS
    -DCMAKE_INSTALL_PREFIX:PATH=<INSTALL_DIR>
    -DCMAKE_INSTALL_LIBDIR:STRING=lib
    -DCMAKE_PREFIX_PATH:PATH=<INSTALL_DIR>:${CMAKE_PREFIX_PATH}
    -DCMAKE_BUILD_TYPE:STRING=${CMAKE_BUILD_TYPE}
    -DBAYEUX_CXX_STANDARD:STRING=17
    -DBAYEUX_COMPILER_ERROR_ON_WARNING:BOOL=OFF
    -DBAYEUX_WITH_DEVELOPER_TOOLS:BOOL=OFF
    -DBAYEUX_WITH_DOCS:BOOL=OFF
    -DBAYEUX_WITH_EMFIELD:BOOL=OFF
    -DBAYEUX_WITH_GEANT4_MODULE:BOOL=OFF
    -DBAYEUX_WITH_QT_GUI:BOOL=OFF
    DEPENDS CAMP_EP
    BUILD_BYPRODUCTS
      ${PROJECT_BINARY_DIR}/BuildProducts/lib/${CMAKE_SHARED_LIBRARY_PREFIX}Bayeux${CMAKE_SHARED_LIBRARY_SUFFIX}
      ${PROJECT_BINARY_DIR}/BuildProducts/include
  )

# - Create "imported" target for Bayeux so we can link to it just as if
#   we'd called find_package(Bayeux).
set(Bayeux_LIBRARY ${PROJECT_BINARY_DIR}/BuildProducts/lib/${CMAKE_SHARED_LIBRARY_PREFIX}Bayeux${CMAKE_SHARED_LIBRARY_SUFFIX})
add_library(Bayeux::Bayeux UNKNOWN IMPORTED)
set_property(TARGET Bayeux::Bayeux PROPERTY IMPORTED_LOCATION ${Bayeux_LIBRARY})
set_property(TARGET Bayeux::Bayeux PROPERTY INTERFACE_INCLUDE_DIRECTORIES "${PROJECT_BINARY_DIR}/BuildProducts/include;${PROJECT_BINARY_DIR}/BuildProducts/include/bayeux")
set_property(TARGET Bayeux::Bayeux PROPERTY INTERFACE_LINK_LIBRARIES "${Boost_LIBRARIES}")
add_dependencies(Bayeux::Bayeux BAYEUX_EP)
